// some of the identifiers used are NML built-ins, some are defined by BANDIT.  No easy way to tell except by searching...

// setup graphics
// trailer_graphics_includer includes a bunch of repeated spritesets and spritegroups, as well as creating a bunch of defines
// it can be over-ridden when custom spritegroups etc are needed (not common) - simply define THIS_TRAILER_GRAPHICS_CHAIN_ID as a custom switch identifier in the file for the vehicle
/*
#ifndef THIS_TRAILER_GRAPHICS_CHAIN_ID
  #include "../templates/trailer_callbacks/trailer_graphics_includer.tnml"
#endif
*/

#include "../tmp/trailer_1_graphics.pnml"
#include "../tmp/trailer_2_graphics.pnml"
#include "../tmp/trailer_3_graphics.pnml"

switch (FEAT_ROADVEHS, SELF, this_trailer_graphics, position_in_consist) {
    1: trailer_1_sg;
    2: trailer_2_sg;
    3: trailer_3_sg;
    return 0xFF; //end cb
}

// assume THIS_TRAILER_CAPACITY_1 is always defined
// if trailers > 1 are not defined, set capacity to 0 (used by cb switches). 
#ifndef THIS_TRAILER_CAPACITY_2
  #define THIS_TRAILER_CAPACITY_2  0
#endif
#ifndef THIS_TRAILER_CAPACITY_3
  #define THIS_TRAILER_CAPACITY_3  0
#endif

// include standard switches to handle trailer capacity cb
// to use a custom capacity cb (e.g. provide date upgrades etc), define THIS_TRAILER_CAPACITY_CB_ID in truck file + provide switches
#ifndef THIS_TRAILER_CAPACITY_CB_ID
  #define THIS_TRAILER_CAPACITY_CB_ID   THIS_VEH_ID(switch_capacity_cb_trailer_)
  // use CPP to create the identifier for an include by concatenating strings (requires many defines to actually do this) 
  // CPP has some opinions about _ character in function names (if _ is used, needs also a trailing _)
  // I thought of a better way to do this, restoring the numeric constants and the #if for this include, and also using #if to define a THIS_TRUCK_TYPE_STRING for use in concatenation  
  // ...but so many people helped me write this that I've left it in place :)
  #define CONCATENATE_CAPACITY_IDENTIFIER_(a, b)  a ## b
  #define THIS_CAPACITY_TEMPLATE_NAME CALL_TEMPLATE_NAME_CONCATENATER_(THIS_TRUCK_TYPE)
  #define CALL_TEMPLATE_NAME_CONCATENATER_(truck_type) CONCATENATE_CAPACITY_IDENTIFIER_(../templates/trailer_callbacks/capacity_cb_trailers_ , truck_type)
  #define QUOTE(a) #a
  #define QUOTE2(a) QUOTE(a)
  #include QUOTE2(THIS_CAPACITY_TEMPLATE_NAME.tnml)
#endif

item(FEAT_ROADVEHS, THIS_TRAILER_ID) {
    property {
        climates_available:             NO_CLIMATE; //hide trailers from buy menu by setting climate availability to 'none'
        cargo_capacity:                 1; //set to 1 - or vehicle not refittable.  Automatically set to correct value with cb36
        sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
        power:                          0; //should be 0 for trailers
        weight:                         0; //should be 0 for trailers
        tractive_effort_coefficient:    0; //should be 0 for trailers
        cost_factor:                    0; //should be 0 for trailers
        running_cost_factor:            0; //should be 0 for trailers
        running_cost_base:              RUNNING_COST_NONE; //should be 0 for trailers
        refittable_cargo_classes:       bitmask(THIS_ALLOWED_CARGO_CLASSES);
        non_refittable_cargo_classes:   bitmask(THIS_DISALLOWED_CARGO_CLASSES);
        cargo_allow_refit:              [THIS_ALLOWED_CARGOS];
        cargo_disallow_refit:           [THIS_DISALLOWED_CARGOS];
    }
    graphics {
        //shorten_vehicle:                THIS_TRAILER_LENGTH;
        cargo_capacity:                 THIS_TRAILER_CAPACITY_CB_ID;
        purchase_cargo_capacity:        0;
        cargo_subtype_text:             THIS_SUBTYPE_TEXT_CB_ID;
        default:                        this_trailer_graphics; //THIS_TRAILER_GRAPHICS_CHAIN_ID;
    }
}

#undef THIS_TRAILER_ID
#undef THIS_TRAILER_LENGTH
#undef THIS_TRAILER_SG_ID

#undef CONCATENATE_CAPACITY_IDENTIFIER_
#undef THIS_CAPACITY_TEMPLATE_NAME
#undef CALL_TEMPLATE_NAME_CONCATENATER_
#undef QUOTE
#undef QUOTE2
