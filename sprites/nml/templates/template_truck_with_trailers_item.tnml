// some of the identifiers used are NML built-ins, some are defined by BANDIT.  No easy way to tell except by searching...
#define THIS_TRAILER_ID           THIS_VEH_ID(trailer_)
#define THIS_ARTICULATED_CB_ID    THIS_VEH_ID(switch_articulated_)

// build trailers
switch (FEAT_ROADVEHS, SELF, THIS_ARTICULATED_CB_ID, extra_callback_info1) {
    1.. THIS_NUM_TRAILERS: return THIS_TRAILER_ID;
    return 0xFF; //stop adding vehicle parts
}

// use CPP to create the identifier for a switch by concatenating strings (requires all 3 defines to do this) 
// CPP has some opinions about _ character in function names (if _ is used, needs also a trailing _)
#define CONCATENATE_SUBTYPE_IDENTIFIER_(a, b, c, d) a ## b ## c ## d // concat whatever is passed as a,b,c,d etc
#define THIS_SUBTYPE_TEXT_CB_ID CALL_SUBTYPE_CONCATENATER_(THIS_NUM_TRAILERS,THIS_TRUCK_TYPE)
#define CALL_SUBTYPE_CONCATENATER_(num,truck_type) CONCATENATE_SUBTYPE_IDENTIFIER_(switch_cb_cargo_subtype_text_, num, _trailer_, truck_type)

// set the vehicle props + switches (handle callbacks etc) 
item(FEAT_ROADVEHS, THIS_VEH_ID()) {
    property {
        name:                           string(THIS_NAME);
        climates_available:             ALL_CLIMATES;
        sound_effect:                   THIS_SOUND_EFFECT;
        visual_effect:                  visual_effect(THIS_SMOKE, THIS_SMOKE_OFFSET);
        introduction_date:              THIS_INTRO_DATE;
        cargo_capacity:                 THIS_CAPACITY;
        sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
        speed:                          THIS_SPEED;
        power:                          THIS_POWER;
        weight:                         THIS_WEIGHT;
        tractive_effort_coefficient:    THIS_TE_COEFFICIENT;
        cost_factor:                    THIS_BUY_COST;
        running_cost_factor:            THIS_RUN_COST;
        refittable_cargo_classes:       bitmask(THIS_ALLOWED_CARGO_CLASSES);
        non_refittable_cargo_classes:   bitmask(THIS_DISALLOWED_CARGO_CLASSES);
        cargo_allow_refit:              [THIS_ALLOWED_CARGOS];
        cargo_disallow_refit:           [THIS_DISALLOWED_CARGOS];
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT);
        model_life:                     THIS_MODEL_LIFE;
        vehicle_life:                   THIS_VEHICLE_LIFE;
    }
    graphics {
        shorten_vehicle:                THIS_LENGTH;
        articulated_part:               THIS_ARTICULATED_CB_ID;
        purchase_cargo_capacity:        THIS_CAPACITY_BUY_MENU;
        cargo_subtype_text:             THIS_SUBTYPE_TEXT_CB_ID;
        default:                        THIS_SG_ID;
    }
}