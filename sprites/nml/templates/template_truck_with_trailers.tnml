// some of the identifiers used are NML built-ins, some are defined by BANDIT.  No easy way to tell except by searching...
#define THIS_ARTICULATED_CB_ID    THIS_VEH_ID(switch_articulated_)
#define THIS_CARGO_SUBTYPE_TEXT_CB_ID  THIS_VEH_ID(switch_cargo_subtype_)


switch (FEAT_ROADVEHS, SELF, THIS_CARGO_SUBTYPE_TEXT_CB_ID, THIS_TRUCK_TYPE) {
    GLOBAL_TRUCK_TYPE_DRAWBAR: return switch_cb_cargo_subtypes_drawbar_truck;
    GLOBAL_TRUCK_TYPE_FIFTH_WHEEL: return switch_cb_cargo_subtypes_fifth_wheel_truck;
    return CB_RESULT_NO_TEXT;
}


#if THIS_NUM_TRAILERS == 1
  switch (FEAT_ROADVEHS, SELF, THIS_ARTICULATED_CB_ID, extra_callback_info1) {
      1: return THIS_TRAILER_1_ID;
      return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
  }
#endif  
#if THIS_NUM_TRAILERS == 2
  switch (FEAT_ROADVEHS, SELF, THIS_ARTICULATED_CB_ID, extra_callback_info1) {
      1: return THIS_TRAILER_1_ID;
      2: return THIS_TRAILER_2_ID;
      return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
  }
#endif  
#if THIS_NUM_TRAILERS == 3
  switch (FEAT_ROADVEHS, SELF, THIS_ARTICULATED_CB_ID, extra_callback_info1) {
      1: return THIS_TRAILER_1_ID;
      2: return THIS_TRAILER_2_ID;
      3: return THIS_TRAILER_3_ID;
      return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
  }
#endif  


 // use CPP to concatenate some identifiers from the vehicle ID and some strings
 // putting these on defines isnt strictly necessary; they could be concatenated inline, but this way helps my brain read other parts of the code
#define THIS_SG_ID   THIS_VEH_ID(sg_)
#define THIS_SS_ID   THIS_VEH_ID(ss_)
spriteset(THIS_SS_ID, THIS_GRAPHICS_FILE) {
  spriteset_template_truck_16(0, 20) // (search 'sprites/nml' to find where spriteset templates are defined)  
}

spritegroup THIS_SG_ID {
    loaded:  [THIS_SS_ID];
    loading: [THIS_SS_ID];
}


// set the vehicle props + switches (handle callbacks etc) 
item(FEAT_ROADVEHS, THIS_VEH_ID(), THIS_NUMERIC_ID) {
    property {
        name:                           string(THIS_NAME);
        climates_available:             ALL_CLIMATES;
        sound_effect:                   THIS_SOUND_EFFECT;
        visual_effect:                  visual_effect(THIS_SMOKE, THIS_SMOKE_OFFSET);
        introduction_date:              THIS_INTRO_DATE;
        cargo_capacity:                 THIS_CAPACITY;
        sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
        speed:                          THIS_SPEED;
        power:                          THIS_POWER;
        weight:                         THIS_WEIGHT;
        tractive_effort_coefficient:    THIS_TE_COEFFICIENT;
        cost_factor:                    THIS_BUY_COST;
        running_cost_factor:            THIS_RUN_COST;
        refittable_cargo_classes:       bitmask(THIS_ALLOWED_CARGO_CLASSES);
        non_refittable_cargo_classes:   bitmask(THIS_DISALLOWED_CARGO_CLASSES);
        cargo_allow_refit:              [THIS_ALLOWED_CARGOS];
        cargo_disallow_refit:           [THIS_DISALLOWED_CARGOS];
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT);
        model_life:                     THIS_MODEL_LIFE;
        vehicle_life:                   THIS_VEHICLE_LIFE;
    }
    graphics {
        shorten_vehicle:                THIS_LENGTH;
        articulated_part:               THIS_ARTICULATED_CB_ID;
        purchase_cargo_capacity:        THIS_CAPACITY_BUY_MENU;
        cargo_subtype_text:             THIS_CARGO_SUBTYPE_TEXT_CB_ID;
        default:                        THIS_SG_ID;
    }
}